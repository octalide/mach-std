use std.types.string;

#@if (OS == OS_LINUX)
use sys: std.system.platform.linux.sys;
#@or (OS == OS_DARWIN)
use sys: std.system.platform.darwin.sys;
#@or (OS == OS_WINDOWS)
use sys: std.system.platform.windows.sys;
#@end

pub val STDIN:  i32 = 0;
pub val STDOUT: i32 = 1;
pub val STDERR: i32 = 2;

#@if (OS == OS_LINUX || OS == OS_DARWIN)

fun platform_write(fd: i32, data: *u8, size: u64) i64 {
    ret sys.write(fd, data, size);
}

fun platform_read(fd: i32, dest: *u8, max: u64) i64 {
    ret sys.read(fd, dest, max);
}

#@or (OS == OS_WINDOWS)

fun windows_handle(fd: i32) *u8 {
    if (fd == STDIN) {
        ret sys.get_std_handle(sys.STD_INPUT_HANDLE);
    }
    or (fd == STDOUT) {
        ret sys.get_std_handle(sys.STD_OUTPUT_HANDLE);
    }
    or (fd == STDERR) {
        ret sys.get_std_handle(sys.STD_ERROR_HANDLE);
    }

    ret nil;
}

fun windows_chunked_io(handle: *u8, buffer: *u8, size: u64, is_write: u8) i64 {
    var remaining: u64 = size;
    var offset: u64 = 0;
    var total: u64 = 0;

    for (remaining > 0) {
        var chunk: u64 = remaining;
        if (chunk > 0xffff_ffff) {
            chunk = 0xffff_ffff;
        }

        var count32: u32 = 0;
        val ok: u32 = if (is_write != 0) {
            sys.write_file(handle, (buffer + offset), chunk :: u32, (?count32) :: *u32)
        } or {
            sys.read_file(handle, (buffer + offset), chunk :: u32, (?count32) :: *u32)
        };

        if (ok == 0) {
            ret -1;
        }

        val processed: u64 = count32 :: u64;
        if (processed == 0) {
            brk;
        }

        offset = offset + processed;
        total = total + processed;
        remaining = remaining - processed;
    }

    ret total :: i64;
}

fun platform_write(fd: i32, data: *u8, size: u64) i64 {
    val handle: *u8 = windows_handle(fd);
    if (handle == nil || (handle :: i64) == -1) {
        ret -1;
    }

    ret windows_chunked_io(handle, data, size, 1);
}

fun platform_read(fd: i32, dest: *u8, max: u64) i64 {
    val handle: *u8 = windows_handle(fd);
    if (handle == nil || (handle :: i64) == -1) {
        ret -1;
    }

    ret windows_chunked_io(handle, dest, max, 0);
}

#@end

fun write_segment(fd: i32, part: string) i64 {
    if (part.length == 0) { ret 0; }
    ret platform_write(fd, part.data, part.length);
}

pub fun write_to(fd: i32, pattern: string, ...) i64 {
    val extra: u64 = va_count();
    if (extra == 0) {
        ret write_segment(fd, pattern);
    }

    val rendered: string = format(pattern, ...);
    val wrote: i64 = write_segment(fd, rendered);
    string_free(rendered);
    ret wrote;
}

pub fun write(pattern: string, ...) i64 {
    ret write_to(STDOUT, pattern, ...);
}

pub fun print(pattern: string, ...) i64 {
    ret write_to(STDOUT, pattern, ...);
}

pub fun error(pattern: string, ...) i64 {
    ret write_to(STDERR, pattern, ...);
}

pub fun read_from(fd: i32, dest: *u8, max: u64) i64 {
    if (dest == nil || max == 0) {
        ret 0;
    }
    ret platform_read(fd, dest, max);
}

pub fun read(dest: *u8, max: u64) i64 {
    ret read_from(STDIN, dest, max);
}
