use std.types.option;
use std.types.string;
use std.types.result;
use std.types.size;

$if (target.os == OS_LINUX) {
    use console: std.system.platform.linux.console;
}

$if (target.os == OS_DARWIN) {
    use console: std.system.platform.darwin.console;
}

$if (target.os == OS_WINDOWS) {
    use console: std.system.platform.windows.console;
}

pub val STDIN:  i32 = 0;
pub val STDOUT: i32 = 1;
pub val STDERR: i32 = 2;

fun write_segment(fd: i32, part: str) i64 {
    if (part.len == 0) {
        ret 0;
    }

    ret console.write(fd, part.data, part.len);
}

pub fun write_to(fd: i32, pattern: str, ...) i64 {
    val extra: usize = va_count();
    if (extra == 0) {
        ret write_segment(fd, pattern);
    }

    val res: Option[String] = format(pattern, ...);
    if (res.is_none()) {
        ret -1;
    }

    var rendered: String = res.unwrap();
    if (rendered.len == 0) {
        rendered.dnit();
        ret 0;
    }

    val view: str = rendered.as_str();
    val wrote: i64 = write_segment(fd, view);
    rendered.dnit();

    ret wrote;
}

pub fun write(pattern: str, ...) i64 {
    ret write_to(STDOUT, pattern, ...);
}

pub fun print(pattern: str, ...) i64 {
    ret write_to(STDOUT, pattern, ...);
}

pub fun error(pattern: str, ...) i64 {
    ret write_to(STDERR, pattern, ...);
}

pub fun read_from(fd: i32, dest: *u8, max: usize) i64 {
    if (dest == nil || max == 0) {
        ret 0;
    }

    ret console.read(fd, dest, max);
}

pub fun read(dest: *u8, max: usize) i64 {
    ret read_from(STDIN, dest, max);
}
