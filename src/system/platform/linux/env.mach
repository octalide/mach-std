use std.types.option;
use std.types.result;
use std.types.string;
use std.io.path;
use std.types.size;
use fs:  std.system.platform.linux.filesystem;
use mem: std.system.memory;

var env_blob_data: *u8;
var env_blob_len:  usize;
var env_loaded:    u8;

fun load_env() u8 {
    val path_res: Result[Path, str] = path_from_string("/proc/self/environ");
    if (path_res.is_err()) {
        env_blob_data = nil;
        env_blob_len  = 0;
        ret 0;
    }

    val path: Path = path_res.unwrap_ok();
    val blob_res: Result[[]u8, i32] = fs.read_file(path);
    path.dnit();

    if (blob_res.is_err()) {
        env_blob_data = nil;
        env_blob_len  = 0;
        ret 0;
    }

    val blob: []u8 = blob_res.unwrap_ok();
    if (blob.data == nil || blob.len == 0) {
        env_blob_data = nil;
        env_blob_len  = 0;
        ret 0;
    }

    env_blob_data = blob.data;
    env_blob_len  = blob.len;
    ret 1;
}

pub fun ensure_env_blob() u8 {
    if (env_loaded != 0) {
        if (env_blob_data != nil && env_blob_len != 0) {
            ret 1;
        }
        ret 0;
    }

    env_loaded = 1;
    ret load_env();
}

pub fun get_env_blob() []u8 {
    ret []u8{ env_blob_data, env_blob_len };
}
