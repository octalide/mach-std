use sys: std.system.platform.windows.sys;

pub rec Timespec {
    tv_sec:  i64;
    tv_nsec: i64;
}

pub rec Tm {
    tm_sec:   i32;  # seconds after the minute [0-60]
    tm_min:   i32;  # minutes after the hour [0-59]
    tm_hour:  i32;  # hours since midnight [0-23]
    tm_mday:  i32;  # day of the month [1-31]
    tm_mon:   i32;  # months since January [0-11]
    tm_year:  i32;  # years since 1900
    tm_wday:  i32;  # days since Sunday [0-6]
    tm_yday:  i32;  # days since January 1 [0-365]
    tm_isdst: i32;  # daylight saving time flag
}

pub rec Filetime {
    low:  u32;
    high: u32;
}

var win_perf_freq:  u64 = 0;
var win_perf_ready: u8  = 0;

fun ensure_win_perf() u8 {
    if (win_perf_ready != 0) {
        ret 1;
    }

    var freq: i64 = 0;
    val ok: u32 = sys.query_performance_frequency((?freq) :: *i64);
    if (ok == 0 || freq <= 0) {
        ret 0;
    }

    win_perf_freq = freq :: u64;
    win_perf_ready = 1;
    ret 1;
}

# get monotonic time in nanoseconds
pub fun monotonic_now() u64 {
    if (ensure_win_perf() == 0) {
        ret 0;
    }

    var counter: i64 = 0;
    val ok: u32 = sys.query_performance_counter((?counter) :: *i64);
    if (ok == 0 || counter < 0) {
        ret 0;
    }

    val value: u64 = counter :: u64;
    ret (value * 1000000000) / win_perf_freq;
}

# get wall clock time in nanoseconds since Unix epoch
# Windows FILETIME is 100-nanosecond intervals since Jan 1, 1601
# Unix epoch (Jan 1, 1970) is 11644473600 seconds after Windows epoch
pub fun realtime_now() u64 {
    var ft: Filetime;
    sys.get_system_time_as_file_time((?ft) :: *u8);

    val ticks:      u64 = ((ft.high :: u64) << 32) | (ft.low :: u64);
    val unix_ticks: u64 = ticks - 116444736000000000;
    val nanos:      u64 = unix_ticks * 100;

    ret nanos;
}

# convert to timespec structure
pub fun realtime_timespec(ts_ptr: *Timespec) i32 {
    val nanos: u64 = realtime_now();
    if (nanos == 0) {
        ret -1;
    }

    ts_ptr.tv_sec  = (nanos / 1000000000) :: i64;
    ts_ptr.tv_nsec = (nanos % 1000000000) :: i64;
    ret 0;
}
