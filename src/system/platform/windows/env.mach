use std.types.option;
use std.types.size;
use mem: std.system.memory;
use sys: std.system.platform.windows.sys;

var env_blob_data: *u8;
var env_blob_len:  usize;
var env_loaded:    u8;

fun load_env() u8 {
    val wide: *u16 = sys.get_environment_strings();
    if (wide == nil) {
        ret 0;
    }

    var units:            usize = 0;
    var bytes:            usize = 0;
    var consecutive_zero: u8    = 0;

    for {
        val ch: u16 = @(wide + units);
        if (ch == 0) {
            bytes = bytes + 1;
            if (consecutive_zero != 0) {
                units = units + 1;
                brk;
            }
            consecutive_zero = 1;
        }
        or {
            if (ch >= 128) {
                sys.free_environment_strings(wide);
                ret 0;
            }
            bytes = bytes + 1;
            consecutive_zero = 0;
        }
        units = units + 1;
    }

    if (bytes == 0) {
        sys.free_environment_strings(wide);
        env_blob_data = nil;
        env_blob_len  = 0;
        ret 1;
    }

    val alloc_res: Option[*u8] = mem.allocate[u8](bytes);
    if (alloc_res.is_none()) {
        sys.free_environment_strings(wide);
        ret 0;
    }

    val buf: *u8 = alloc_res.unwrap();

    var src:              usize = 0;
    var dst:              usize = 0;
    consecutive_zero = 0;
    for (src < units) {
        val ch: u16 = @(wide + src);
        src = src + 1;

        if (ch == 0) {
            @(buf + dst) = 0;
            dst = dst + 1;
            if (consecutive_zero != 0) {
                brk;
            }
            consecutive_zero = 1;
        }
        or {
            @(buf + dst) = ch :: u8;
            dst = dst + 1;
            consecutive_zero = 0;
        }
    }

    sys.free_environment_strings(wide);

    env_blob_data = buf;
    env_blob_len  = dst;
    ret 1;
}

pub fun ensure_env_blob() u8 {
    if (env_loaded != 0) {
        if (env_blob_data != nil && env_blob_len != 0) {
            ret 1;
        }
        ret 0;
    }

    env_loaded = 1;
    ret load_env();
}

pub fun get_env_blob() []u8 {
    ret []u8{ env_blob_data, env_blob_len };
}
