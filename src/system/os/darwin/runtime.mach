# darwin (macos/ios) runtime implementation

use      std.types.option;
use      std.types.size;
use      std.types.string;
use      std.collections.slice;
use mem: std.system.memory;

# architecture dispatch
$if ($mach.build.target.arch.id == $mach.arch.x86_64.id) {
    use arch: std.system.arch.x86_64;
}
$if ($mach.build.target.arch.id == $mach.arch.aarch64.id) {
    use arch: std.system.arch.aarch64;
}

# syscall numbers for darwin (x86_64)
$if ($mach.build.target.arch.id == $mach.arch.x86_64.id) {
    val SYS_EXIT: u64 = 0x2000001;
    val SYS_WRITE: u64 = 0x2000004;
}

# syscall numbers for darwin (aarch64)
$if ($mach.build.target.arch.id == $mach.arch.aarch64.id) {
    val SYS_EXIT: u64 = 1;
    val SYS_WRITE: u64 = 4;
}

val WORD_BYTES: usize = arch.WORD_SIZE;

fun cstring_length(s: cstr) usize {
    if (s == nil) {
        ret 0;
    }

    var length: usize = 0;
    var cursor: cstr  = s;
    for {
        if (cursor[0] == 0::char) {
            brk;
        }
        length = length + 1;
        cursor = cursor + 1;
    }

    ret length;
}

pub fun build_args(argc: usize, stack: *u8) Option[Slice[str]] {
    if (argc == 0) {
        ret some[Slice[str]](Slice[str]{ nil, 0 });
    }

    val entry_size:  usize = $size_of(str);
    val total_bytes: usize = argc * entry_size;
    if (total_bytes == 0) {
        ret some[Slice[str]](Slice[str]{ nil, 0 });
    }

    val storage_opt: Option[*u8] = mem.allocate[u8](total_bytes);
    if (storage_opt.is_none()) {
        ret none[Slice[str]]();
    }

    val storage: *u8 = storage_opt.unwrap();

    val argv_base: *u8 = stack + WORD_BYTES;

    var index: usize = 0;
    for (index < argc) {
        val offset:  usize = index * WORD_BYTES;
        val addr:    *u8   = argv_base + offset;
        val raw:     usize = (addr::*usize)[0];
        val arg_ptr: cstr  = raw::cstr;
        val length:  usize = cstring_length(arg_ptr);

        val slot_offset: usize = index * entry_size;
        val slot_addr:   *u8   = storage + slot_offset;

        val data_slot: **char = slot_addr::**char;
        data_slot[0] = arg_ptr;

        val length_addr: *u8     = slot_addr + WORD_BYTES;
        val length_slot: *usize = length_addr::*usize;
        length_slot[0] = length;

        index = index + 1;
    }

    ret some[Slice[str]](Slice[str]{ storage::*str, argc });
}

pub fun get_argc(stack: *u8) usize {
    ret (stack::*usize)[0];
}

pub fun exit(code: i64) {
    arch.syscall1(SYS_EXIT, code::u64);
    arch.halt();
}

pub fun write(fd: i32, buf: *u8, count: usize) i64 {
    val result: u64 = arch.syscall3(SYS_WRITE, fd::u64, buf::ptr::u64, count::u64);
    ret result::i64;
}
