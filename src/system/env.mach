use std.types.string;
use std.types.size;

$if (OS == OS_LINUX) {
    use platform: std.system.platform.linux.env;
}

$if (OS == OS_DARWIN) {
    use platform: std.system.platform.darwin.env;
}

$if (OS == OS_WINDOWS) {
    use platform: std.system.platform.windows.env;
}

fun find_value(blob: []u8, name: str) []u8 {
    if (blob.data == nil || blob.len == 0) {
        ret []u8{ nil, 0 };
    }

    val target_len: usize = name.len;
    val data:       *u8   = blob.data;
    val total:      usize = blob.len;

    var pos: usize = 0;
    for (pos < total) {
        val segment_start: usize = pos;

        for (pos < total && @(data + pos) != 0) {
            pos = pos + 1;
        }

        val segment_end: usize = pos;
        val segment_len: usize = segment_end - segment_start;

        if (segment_len > target_len) {
            var match: u8    = 1;
            var idx:   usize = 0;

            for (idx < target_len) {
                val a: u8 = @(data + segment_start + idx);
                val b: u8 = @(name.data + idx);

                if (a != b) {
                    match = 0;
                    brk;
                }

                idx = idx + 1;
            }

            if (match == 1 && (segment_start + target_len) < segment_end && @(data + segment_start + target_len) == '=') {
                val value_start: usize = segment_start + target_len + 1;
                val value_len:   usize = segment_len - target_len - 1;

                ret []u8{ data + value_start, value_len };
            }
        }

        if (pos >= total) {
            brk;
        }

        pos = pos + 1;
    }

    ret []u8{ nil, 0 };
}

pub fun get(name: str) []u8 {
    if (name.len == 0) {
        ret []u8{ nil, 0 };
    }

    if (platform.ensure_env_blob() == 0) {
        ret []u8{ nil, 0 };
    }

    val blob: []u8 = platform.get_env_blob();
    ret find_value(blob, name);
}
